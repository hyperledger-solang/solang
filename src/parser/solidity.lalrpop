
use std::str::FromStr;
use num_bigint::BigInt;
use parser::ast::*;
use parser::box_option;

grammar;

pub SourceUnit: SourceUnit = {
    SourceUnitPart+ => SourceUnit(<>)
}

SourceUnitPart: SourceUnitPart = {
    ContractDefinition => SourceUnitPart::ContractDefinition(<>),
    PragmaDirective => SourceUnitPart::PragmaDirective(<>.0, <>.1),
    ImportDirective => SourceUnitPart::ImportDirective(<>),
}

ImportDirective: String = {
    "import" <s:StringLiteral> ";" => s
}

PragmaDirective: (Identifier, String) = {
    "pragma" <i:Identifier> <s:PragmaValue> ";" => (i, s)
}

// This is actually defined as [^;]*, however that causes ambiguity for the lexer. So define it
// in a much more restricted way. Hopefully lalrpop will restrict the lexer to only those tokens
// possible during parsing.
PragmaValue: String = {
    r"\^0\.\d+\.\d+" => <>.to_string()
}

ElementaryTypeName: ElementaryTypeName = {
    "bool" => ElementaryTypeName::Bool,
    "address" => ElementaryTypeName::Address,
    "string" => ElementaryTypeName::String,
    "uint" => ElementaryTypeName::Uint(256),
    "uint8" => ElementaryTypeName::Uint(8),
    // for i in $(seq 8 8 256); do printf '    "uint%d" => ElementaryTypeName::Uint(%d),\n' $i $i; done
    "uint16" => ElementaryTypeName::Uint(16),
    "uint24" => ElementaryTypeName::Uint(24),
    "uint32" => ElementaryTypeName::Uint(32),
    "uint40" => ElementaryTypeName::Uint(40),
    "uint48" => ElementaryTypeName::Uint(48),
    "uint56" => ElementaryTypeName::Uint(56),
    "uint64" => ElementaryTypeName::Uint(64),
    "uint72" => ElementaryTypeName::Uint(72),
    "uint80" => ElementaryTypeName::Uint(80),
    "uint88" => ElementaryTypeName::Uint(88),
    "uint96" => ElementaryTypeName::Uint(96),
    "uint104" => ElementaryTypeName::Uint(104),
    "uint112" => ElementaryTypeName::Uint(112),
    "uint120" => ElementaryTypeName::Uint(120),
    "uint128" => ElementaryTypeName::Uint(128),
    "uint136" => ElementaryTypeName::Uint(136),
    "uint144" => ElementaryTypeName::Uint(144),
    "uint152" => ElementaryTypeName::Uint(152),
    "uint160" => ElementaryTypeName::Uint(160),
    "uint168" => ElementaryTypeName::Uint(168),
    "uint176" => ElementaryTypeName::Uint(176),
    "uint184" => ElementaryTypeName::Uint(184),
    "uint192" => ElementaryTypeName::Uint(192),
    "uint200" => ElementaryTypeName::Uint(200),
    "uint208" => ElementaryTypeName::Uint(208),
    "uint216" => ElementaryTypeName::Uint(216),
    "uint224" => ElementaryTypeName::Uint(224),
    "uint232" => ElementaryTypeName::Uint(232),
    "uint240" => ElementaryTypeName::Uint(240),
    "uint248" => ElementaryTypeName::Uint(248),
    "uint256" => ElementaryTypeName::Uint(256),
    "int" => ElementaryTypeName::Int(256),
    "int8" => ElementaryTypeName::Int(8),
    "int16" => ElementaryTypeName::Int(16),
    "int24" => ElementaryTypeName::Int(24),
    "int32" => ElementaryTypeName::Int(32),
    "int40" => ElementaryTypeName::Int(40),
    "int48" => ElementaryTypeName::Int(48),
    "int56" => ElementaryTypeName::Int(56),
    "int64" => ElementaryTypeName::Int(64),
    "int72" => ElementaryTypeName::Int(72),
    "int80" => ElementaryTypeName::Int(80),
    "int88" => ElementaryTypeName::Int(88),
    "int96" => ElementaryTypeName::Int(96),
    "int104" => ElementaryTypeName::Int(104),
    "int112" => ElementaryTypeName::Int(112),
    "int120" => ElementaryTypeName::Int(120),
    "int128" => ElementaryTypeName::Int(128),
    "int136" => ElementaryTypeName::Int(136),
    "int144" => ElementaryTypeName::Int(144),
    "int152" => ElementaryTypeName::Int(152),
    "int160" => ElementaryTypeName::Int(160),
    "int168" => ElementaryTypeName::Int(168),
    "int176" => ElementaryTypeName::Int(176),
    "int184" => ElementaryTypeName::Int(184),
    "int192" => ElementaryTypeName::Int(192),
    "int200" => ElementaryTypeName::Int(200),
    "int208" => ElementaryTypeName::Int(208),
    "int216" => ElementaryTypeName::Int(216),
    "int224" => ElementaryTypeName::Int(224),
    "int232" => ElementaryTypeName::Int(232),
    "int240" => ElementaryTypeName::Int(240),
    "int248" => ElementaryTypeName::Int(248),
    "int256" => ElementaryTypeName::Int(256),
    "byte" => ElementaryTypeName::Bytes(1),
    "bytes" => ElementaryTypeName::DynamicBytes,
    "bytes1" => ElementaryTypeName::Bytes(1),
    "bytes2" => ElementaryTypeName::Bytes(2),
    "bytes3" => ElementaryTypeName::Bytes(3),
    "bytes4" => ElementaryTypeName::Bytes(4),
    "bytes5" => ElementaryTypeName::Bytes(5),
    "bytes6" => ElementaryTypeName::Bytes(6),
    "bytes7" => ElementaryTypeName::Bytes(7),
    "bytes8" => ElementaryTypeName::Bytes(8),
    "bytes9" => ElementaryTypeName::Bytes(9),
    "bytes10" => ElementaryTypeName::Bytes(10),
    "bytes11" => ElementaryTypeName::Bytes(11),
    "bytes12" => ElementaryTypeName::Bytes(12),
    "bytes13" => ElementaryTypeName::Bytes(13),
    "bytes14" => ElementaryTypeName::Bytes(14),
    "bytes15" => ElementaryTypeName::Bytes(15),
    "bytes16" => ElementaryTypeName::Bytes(16),
    "bytes17" => ElementaryTypeName::Bytes(17),
    "bytes18" => ElementaryTypeName::Bytes(18),
    "bytes19" => ElementaryTypeName::Bytes(19),
    "bytes20" => ElementaryTypeName::Bytes(20),
    "bytes21" => ElementaryTypeName::Bytes(21),
    "bytes22" => ElementaryTypeName::Bytes(22),
    "bytes23" => ElementaryTypeName::Bytes(23),
    "bytes24" => ElementaryTypeName::Bytes(24),
    "bytes25" => ElementaryTypeName::Bytes(25),
    "bytes26" => ElementaryTypeName::Bytes(26),
    "bytes27" => ElementaryTypeName::Bytes(27),
    "bytes28" => ElementaryTypeName::Bytes(28),
    "bytes29" => ElementaryTypeName::Bytes(29),
    "bytes30" => ElementaryTypeName::Bytes(30),
    "bytes31" => ElementaryTypeName::Bytes(31),
    "bytes32" => ElementaryTypeName::Bytes(32),
}

TypeName: TypeName = {
    ElementaryTypeName => TypeName::Elementary(<>),
    Identifier => TypeName::Unresolved(<>),
}

StorageLocation: StorageLocation = {
    "memory" => StorageLocation::Memory,
    "storage" => StorageLocation::Storage,
    "calldata" => StorageLocation::Calldata,
}

Identifier: Identifier = {
    <l:@L> <n:r"[a-zA-Z_$][a-zA-Z_$0-9]*"> <r:@R> => Identifier{loc: Loc(l, r), name: n.to_string()}
}

VariableDeclaration: Box<VariableDeclaration> = {
    <t:TypeName> <s:StorageLocation> <i:Identifier> => Box::new(VariableDeclaration{
        typ: t, storage: s, name: i
    }),
    <t:TypeName> <i:Identifier> => Box::new(VariableDeclaration{ 
        typ: t, storage: StorageLocation::Default, name: i
    })
}

StructDefinition: Box<StructDefinition> = {
    "struct" <n:Identifier> "{" <v:(<VariableDeclaration> ";")+> "}" => {
        Box::new(StructDefinition{name: n, fields: v})
    }
}

ContractType: ContractType = {
    "contract" => ContractType::Contract,
    "interface" => ContractType::Interface,
    "library" => ContractType::Library,
}

ContractPart: ContractPart = {
    StructDefinition => ContractPart::StructDefinition(<>),
    EventDefinition => ContractPart::EventDefinition(<>),
    EnumDefinition => ContractPart::EnumDefinition(<>),
    ContractVariableDefinition => ContractPart::ContractVariableDefinition(<>),
    FunctionDefinition => ContractPart::FunctionDefinition(<>),
}

ContractDefinition: Box<ContractDefinition> = {
    <l:@L> <t:ContractType> <n:Identifier> "{" <p:(<ContractPart>)+> "}" <r:@R> => {
        Box::new(ContractDefinition{loc: Loc(l, r), ty: t, name: n, parts: p})
    }
}

EventParameter: EventParameter = {
    <t:TypeName> <i:"indexed"?> <n:Identifier?> => EventParameter{
        typ: t, indexed: match i { Some(_) => true, _ => false }, name:n
    }
}

EventDefinition: Box<EventDefinition> = {
    "event" <n:Identifier> "(" <v:(<EventParameter> ",")*> <e:EventParameter> ")" <a:"anonymous"?> ";" => {
        let mut v = v;
        v.push(e);
        Box::new(EventDefinition{
            name: n, fields: v, anonymous: match a { Some(_) => true, _ => false}
        })
    },
}

EnumDefinition: Box<EnumDefinition> = {
    "enum" <n:Identifier> "{" <v:(<Identifier> ",")*> <e:Identifier> "}" => {
        let mut v = v;
        v.push(e);
        Box::new(EnumDefinition{name: n, values: v})
    }
}

ContractVariableDefinition: Box<ContractVariableDefinition> = {
    <l:@L> <t:TypeName> <a:VariableAttribute*> <i:Identifier> <e:("=" <Expression>)?> <r:@R>";" => {
        Box::new(ContractVariableDefinition{
            loc: Loc(l, r), ty: t, attrs: a, name: i, initializer: e,
        })
    }
}

Visibility: Visibility = {
    <l:@L> "public" <r:@R> => Visibility::Public(Loc(l, r)),
    <l:@L> "external" <r:@R> => Visibility::External(Loc(l, r)),
    <l:@L> "internal" <r:@R> => Visibility::Internal(Loc(l, r)),
    <l:@L> "private" <r:@R> => Visibility::Private(Loc(l, r)),
}

VariableAttribute: VariableAttribute = {
    Visibility => VariableAttribute::Visibility(<>),
    <l:@L> "constant" <r:@R> => VariableAttribute::Constant(Loc(l, r))
}

Expression: Expression = {
    Precedence15,
}

Precedence15: Expression = {
    <l:Precedence15> <a:@L> "=" <b:@R> <r:Precedence14> => Expression::Assign(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "|=" <b:@R> <r:Precedence14> => Expression::AssignOr(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "^=" <b:@R> <r:Precedence14> => Expression::AssignXor(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "&=" <b:@R> <r:Precedence14> => Expression::AssignAnd(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "<<=" <b:@R> <r:Precedence14> => Expression::AssignShiftLeft(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> ">>=" <b:@R> <r:Precedence14> => Expression::AssignShiftRight(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "+=" <b:@R> <r:Precedence14> => Expression::AssignAdd(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "-=" <b:@R> <r:Precedence14> => Expression::AssignSubtract(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "*=" <b:@R> <r:Precedence14> => Expression::AssignMultiply(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "/=" <b:@R> <r:Precedence14> => Expression::AssignDivide(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence15> <a:@L> "%=" <b:@R> <r:Precedence14> => Expression::AssignModulo(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence14,
}

Precedence14: Expression = {
    <c:Precedence14> <a:@L> "?" <l:Precedence13> ":" <b:@R> <r:Precedence13> => Expression::Ternary(Loc(a, b), Box::new(c), Box::new(l), Box::new(r)),
    Precedence13,
}

Precedence13: Expression = {
    <l:Precedence13> <a:@L> "||" <b:@R> <r:Precedence12> => Expression::Or(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence12,
}

Precedence12: Expression = {
    <l:Precedence12> <a:@L> "&&" <b:@R> <r:Precedence11> => Expression::And(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence11,
}

Precedence11: Expression = {
    <l:Precedence11> <a:@L> "==" <b:@R> <r:Precedence10> => Expression::Equal(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence11> <a:@L> "!=" <b:@R> <r:Precedence10> => Expression::NotEqual(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence10,
}

Precedence10: Expression = {
    <l:Precedence10> <a:@L> "<" <b:@R> <r:Precedence9> => Expression::Less(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence10> <a:@L> ">" <b:@R> <r:Precedence9> => Expression::More(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence10> <a:@L> "<=" <b:@R> <r:Precedence9> => Expression::LessEqual(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence10> <a:@L> ">=" <b:@R> <r:Precedence9> => Expression::MoreEqual(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence9,
}

Precedence9: Expression = {
    <l:Precedence9> <a:@L> "|" <b:@R> <r:Precedence8> => Expression::BitwiseOr(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence8,
}

Precedence8: Expression = {
    <l:Precedence8> <a:@L> "^" <b:@R> <r:Precedence7> => Expression::BitwiseXor(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence7,
}

Precedence7: Expression = {
    <l:Precedence7> <a:@L> "&" <b:@R> <r:Precedence6> => Expression::BitwiseAnd(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence6,
}

Precedence6: Expression = {
    <l:Precedence6> <a:@L> "<<" <b:@R> <r:Precedence5> => Expression::ShiftLeft(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence6> <a:@L> ">>" <b:@R> <r:Precedence5> => Expression::ShiftRight(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence5,
}

Precedence5: Expression = {
    <l:Precedence5> <a:@L> "+" <b:@R> <r:Precedence4> => Expression::Add(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence5> <a:@L> "-" <b:@R> <r:Precedence4> => Expression::Subtract(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence4,
}

Precedence4: Expression = {
    <l:Precedence4> <a:@L> "*" <b:@R> <r:Precedence3> => Expression::Multiply(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence4> <a:@L> "/" <b:@R> <r:Precedence3> => Expression::Divide(Loc(a, b), Box::new(l), Box::new(r)),
    <l:Precedence4> <a:@L> "%" <b:@R> <r:Precedence3> => Expression::Modulo(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence3,
}

Precedence3: Expression = {
    <l:Precedence3> <a:@L> "**" <b:@R> <r:Precedence2> => Expression::Power(Loc(a, b), Box::new(l), Box::new(r)),
    Precedence2,
}

Precedence2: Expression = {
    <a:@L> "!" <b:@R> <e:Precedence2> => Expression::Not(Loc(a, b), Box::new(e)),
    <a:@L> "~" <b:@R> <e:Precedence2> => Expression::Complement(Loc(a, b), Box::new(e)),
    <a:@L> "delete" <b:@R> <e:Precedence2> => Expression::Delete(Loc(a, b), Box::new(e)),
    <a:@L> "++" <b:@R> <e:Precedence2> => Expression::PreIncrement(Loc(a, b), Box::new(e)),
    <a:@L> "--" <b:@R> <e:Precedence2> => Expression::PreDecrement(Loc(a, b), Box::new(e)),
    <a:@L> "+" <b:@R> <e:Precedence2> => Expression::UnaryPlus(Loc(a, b), Box::new(e)),
    <a:@L> "-" <b:@R> <e:Precedence2> => Expression::UnaryMinus(Loc(a, b), Box::new(e)),
    Precedence1,
}

Precedence1: Expression = {
    <a:@L> <e:Precedence1> "++" <b:@R> => Expression::PostIncrement(Loc(a, b), Box::new(e)),
    <a:@L> <e:Precedence1> "--" <b:@R> => Expression::PostDecrement(Loc(a, b), Box::new(e)),
    <a:@L> "new" <t:ElementaryTypeName> <b:@R> => Expression::New(Loc(a, b), t),
    <a:@L> <e:Precedence1> "[" <i:Expression?> "]" <b:@R> => Expression::IndexAccess(Loc(a, b), Box::new(e), box_option(i)),
    <a:@L> <e:Precedence1> "." <i:Identifier> <b:@R> => Expression::MemberAccess(Loc(a, b), Box::new(e), i),
    <a:@L> <i:TypeName> "(" ")" <b:@R> => {
        Expression::FunctionCall(Loc(a, b), i, Vec::new())
    },
    <a:@L> <i:TypeName> "(" <v:(<Expression> ",")*> <e:Expression> ")" <b:@R> => {
        let mut v = v;
        v.push(e);
        Expression::FunctionCall(Loc(a, b), i, v)
    },
    <a:@L> "true" <b:@R> => Expression::BoolLiteral(Loc(a, b), true),
    <a:@L> "false" <b:@R> => Expression::BoolLiteral(Loc(a, b), false),
    <a:@L> <s:StringLiteral> <b:@R> => Expression::StringLiteral(Loc(a, b), s.to_string()),
    <a:@L> <s:HexLiteral> <b:@R> => Expression::HexLiteral(Loc(a, b), s.to_string()),
    <i:Identifier> => Expression::Variable(i),
    "(" <e:Expression> ")" => e,
    <l:@L> <n:r#"-?[0-9]+"#> <r:@R> => Expression::NumberLiteral(Loc(l, r), BigInt::from_str(n).unwrap()),
 }

StringLiteral: String = {
    r#""([^"]|'\\'.)*""# => {
        let mut v = <>.to_string();
        v.remove(0);
        let len = v.len();
        v.remove(len - 1);
        v.replace("\\\n", "")
    }
}

HexLiteral: String = {
    r#"hex"[0-9a-fA-F]*""# => {
        let v = <>.to_string();
        let hex_len = v.len() - 5;
        v.chars().skip(4).take(hex_len).collect()
    }
}

Parameter: Parameter = {
    <e:TypeName> <s:StorageLocation?> <i:Identifier?> => {
        Parameter{typ: e, storage: s, name: i}
    }
}

ParameterList: Vec<Parameter> = {
    "(" ")" => Vec::new(),
    "(" <v:(<Parameter> ",")*> <e:Parameter> ")" => {
        let mut v = v;
        v.push(e);
        v
    }
}

StateMutability: StateMutability = {
    <l:@L> "pure" <r:@R> => StateMutability::Pure(Loc(l, r)),
    <l:@L> "view" <r:@R> => StateMutability::View(Loc(l, r)),
    <l:@L> "payable" <r:@R> => StateMutability::Payable(Loc(l, r)),
}

FunctionAttribute: FunctionAttribute = {
    StateMutability => FunctionAttribute::StateMutability(<>),
    Visibility => FunctionAttribute::Visibility(<>),
}

FunctionDefinition: Box<FunctionDefinition> = {
    <l:@L> "constructor" <a:ParameterList> <t:FunctionAttribute*> <r:("returns" <ParameterList>)?> <b:BlockStatement> <e:@R> => {
        let returns = match r {
            None => vec!(),
            Some(v) => v,
        };
        
        Box::new(FunctionDefinition{
            loc: Loc(l, e),
            constructor: true,
            name: None,
            params: a,
            attributes: t,
            returns: returns,
            body: Statement::BlockStatement(b),
        })
    },
    <l:@L> "function" <n:Identifier?> <a:ParameterList> <t:FunctionAttribute*> <r:("returns" <ParameterList>)?> ";" <e:@R> => {
        let returns = match r {
            None => vec!(),
            Some(v) => v,
        };
        
        Box::new(FunctionDefinition{
            loc: Loc(l, e),
            constructor: false,
            name: n,
            params: a,
            attributes: t,
            returns: returns,
            body: Statement::Empty,
        })
    },
    <l:@L> "function" <n:Identifier?> <a:ParameterList> <t:FunctionAttribute*> <r:("returns" <ParameterList>)?> <b:BlockStatement> <e:@R> => {
        let returns = match r {
            None => vec!(),
            Some(v) => v,
        };
        
        Box::new(FunctionDefinition{
            loc: Loc(l, e),
            constructor: false,
            name: n,
            params: a,
            attributes: t,
            returns: returns,
            body: Statement::BlockStatement(b),
        })
    }
}

BlockStatement: BlockStatement = {
    "{" <s:Statement*> "}" => {
        BlockStatement(s)
    },
}

OpenStatement: Statement = {
    "if" "(" <cond:Expression> ")" <body:Statement> => {
        Statement::If(cond, Box::new(body), None)
    },
    "if" "(" <cond:Expression> ")" <body:ClosedStatement> "else" <o:OpenStatement> => {
        Statement::If(cond, Box::new(body), Some(Box::new(o)))
    },
    "while" "(" <e:Expression> ")" <b:OpenStatement> => {
        Statement::While(e, Box::new(b))
    }
}

ClosedStatement: Statement = {
    NonIfStatement,
    "if" "(" <cond:Expression> ")" <body:ClosedStatement> "else" <o:ClosedStatement> => {
        Statement::If(cond, Box::new(body), Some(Box::new(o)))
    },
    "while" "(" <e:Expression> ")" <b:ClosedStatement> => {
        Statement::While(e, Box::new(b))
    },
    "for" "(" <b:SimpleStatement?> ";" <c:Expression?> ";" <n:SimpleStatement?> ")" <block:ClosedStatement> => {
        Statement::For(box_option(b), box_option(c), box_option(n), Some(Box::new(block)))
    },
    "for" "(" <b:SimpleStatement?> ";" <c:Expression?> ";" <n:SimpleStatement?> ")" ";" => {
        Statement::For(box_option(b), box_option(c), box_option(n), None)
    }
}

Statement: Statement = {
    OpenStatement,
    ClosedStatement
}

SimpleStatement: Statement = {
    <v:VariableDeclaration> <e:("=" <Expression>)?> => {
        Statement::VariableDefinition(v, e)
    },
    <e:Expression> => {
        Statement::Expression(e)
    }
}

NonIfStatement: Statement = {
    BlockStatement => {
        Statement::BlockStatement(<>)
    },
    <b:SimpleStatement> ";" => b,
    "_" ";" => {
        Statement::PlaceHolder
    },
    "do" <b:Statement> "while" "(" <e:Expression> ")" ";" => {
        Statement::DoWhile(Box::new(b), e)
    },
    "continue" ";" => {
        Statement::Continue
    },
    "break" ";" => {
        Statement::Break
    },
    <l:@L> "return" <r:@R> ";" => {
        Statement::Return(Loc(l, r), Vec::new())
    },
    <l:@L> "return" <e:Expression> <r:@R> ";" => {
        Statement::Return(Loc(l, r), vec!(e))
    },
    <l:@L> "return" "(" <v:(<Expression> ",")+> <e:Expression> ")" <r:@R> ";" => {
        let mut v = v;
        v.push(e);
        Statement::Return(Loc(l, r), v)
    },
    "throw" ";" => {
        Statement::Throw
    },
    "emit" <n:Identifier> "(" ")" ";" => {
        Statement::Emit(n, Vec::new())
    },
    "emit" <n:Identifier> "(" <v:(<Expression> ",")+> <e:Expression> ")" ";" => {
        let mut v = v;
        v.push(e);
        Statement::Emit(n, v)
    }
}